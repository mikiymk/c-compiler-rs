num_non_zero := "1" .. "9";

num_with_zero := "0" .. "9";

num := num_non_zero (num_with_zero)*;

char := "a" .. "z";


program := function*;

function := "int" identify "(" (declaration ("," declaration)*)? ")" statement;

statement := expression ";"
    | declaration ";"
    | "return" expression ";"
    | "if" "(" expression ")" statement ("else" statement)?
    | "while" "(" expression ")" statement
    | "for" "(" expression? ";" expression? ";" expression? ")" statement
    | "{" statement* "}";

expression := assign;

assign := equality ("=" assign)?;

equality := relational ("==" relational | "!=" relational)*;

relational := add ("<" add | "<=" add | ">" add | ">=" add)*;

add := mul ("+" mul | "-" mul)*;

mul := unary ("*" unary | "/" unary)*;

unary := "+"? primary
    | "-" primary
    | "*" unary
    | "&" unary
    | "sizeof" unary;

primary := num
    | identify ("(" (expression ("," expression)*)? ")")?
    | "(" expression ")";

declaration := "int" "*"* identify;

identify := char+;
